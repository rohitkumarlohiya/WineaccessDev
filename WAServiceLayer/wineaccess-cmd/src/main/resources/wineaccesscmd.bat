@ECHO OFF
REM wineaccesscmd.bat
REM
REM

SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
SET DEBUG_ECHO=REM
REM Uncomment the next line for debug messages
REM SET DEBUG_ECHO=ECHO

%DEBUG_ECHO% All Arguements: %1 %2 %3 %4 %5 %6 %7 %8 %9
SET WINEACCESSCMD_CLASS=com.wineaccess.cmd.WACmd

SET WINEACCESSCMD_NAME=%~n0
%DEBUG_ECHO% WINEACCESSCMD_NAME: %WINEACCESSCMD_NAME%
SET WINEACCESSCMD_HOME=%~dp0
%DEBUG_ECHO% WINEACCESSCMD_HOME: %WINEACCESSCMD_HOME%

SET JAVACMD=java
IF NOT "%JAVA_HOME%" == "" (
	SET JAVACMD="%JAVA_HOME%\bin\java"
)
SET CLASSPATH=%WINEACCESSCMD_HOME%
FOR %%A IN (%WINEACCESSCMD_HOME%..\lib\*.jar) DO SET CLASSPATH=!CLASSPATH!;%%A
%DEBUG_ECHO% CLASSPATH: %CLASSPATH%

SET v1=%1
SET v2=%2
SET v3=%3
SET v4=%4
SET v5=%5
SET v6=%6
SHIFT
SHIFT
SHIFT
SHIFT
SHIFT
SHIFT


REM fire actual command
%DEBUG_ECHO% %JAVACMD% %JAVA_OPT_DEFS% %WINEACCESSCMD_CLASS% %v1% %v2% %v3% %v4% %v5% %v6% %1 %2 %3 %4 %5 %6 %7 %8 %9
%JAVACMD% %JAVA_OPT_DEFS% %WINEACCESSCMD_CLASS% %v1% %v2% %v3% %v4% %v5% %v6% %1 %2 %3 %4 %5 %6 %7 %8 %9
GOTO :EOF

ENDLOCAL
